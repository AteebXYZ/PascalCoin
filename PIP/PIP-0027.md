<pre>
  PIP: PIP-0027
  Title: E-PASA: Layer-2 Addresses
  Type: Frontend
  Impact: None
  Author: Herman Schoenfeld <herman@sphere10.com>
  Comments-URI: https://discord.gg/sJqcgtD  (channel #pip-0027)
  Status: Proposed
  Created: 2019-02-11
</pre>

## Summary

This PIP proposes a backwards compatible addressing scheme to facilitate Layer-2 addresses as well as improve current Layer-1 payments and general usage.

## Motivation

PascalCoin current allows users to send/receive operations between accounts using their account numbers (PASA) as a reference. These account numbers are typically post-fixed with a checksum to avoid data entry errors. 

This proposal extends this specification to allow the embedding of a payload to an address. This allows a recipient to specify the payload the sender must enter when sending an operation. This is useful in many current use-cases such as a merchant specifying the payload code in the buyers transaction so that he can recognize it. Also, for exchanges to specify the payload for users to deposit. By using these addresses, a single PASA can be represented in many forms. 

Most importantly, Layer-2 applications require this as an addressing scheme for Layer-2 applications. As a result, this PIP proposes a formal specification for these addresses.

## Specification

Layer-2 addresses will be herein referred to as Extended PASA, or E-PASA for short.

### Extended PASA format (E-PASA)

An Extended PASA is defined by the below EBNF grammar:

```
    EPASA              = PASA, [ ExtendedAddress ] ;
    PASA               = ( AccountName | AccountNumber ) ;
    AccountName        = Pascal64String 
    AccountNumber      = Integer, "-", Checksum ;
    Checksum           = Digit, Digit ;
    ExtendedAddress    = ( PublicPayload | ReceiverEncPayload | SenderEncPayload | PasswordEncPayload ), [ ":", PayloadChecksum ] ;
    PublicPayload      = "[", Payload, "]" ;
    ReceiverEncPayload = "(", Payload, ")" ;
    SenderEncPayload   = "<", Payload, ">" ;
    PasswordEncPayload = "{", Payload, ":", Password, "}" ;
    Payload            = [ """, SafeAnsiString, """ | HexString | Base58String ] ;      (* allows empty payload *)
    PayloadChecksum    = HexByte, HexByte
    Password           = SafeAnsiString
    SafeAnsiString     = SafeAnsiChar, { SafeAnsiChar } ;
    SafeAnsiChar       = (" " | "!" | EscapeChar """ | "#" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "[" | EscapeChar "\" | "]" | "^" | "_" | "`" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "{" | "|" | EscapeChar "}" | "~") ;
    Pascal64String     = SafePascal64Char, {Pascal64Char}
    Pascal64Char       = (Digit | SafePascal64Char)
    SafePascal64Char   = ( "a", "b", "c", "d", "e", "f", "g", "h", "i", "j", "k", "l", "m", "n", "o", "p", "q", "r", "s", "t", "u", "v", "w", "x", "y", "z", "!", "@", "#", "$", "%", "^", "&", "*", "(", ")", "-", "+", "{", "}", "[", "]", "_", ":", "`", "|", "<", ">", ",", ".", "?", "/", "~", ")" "-", "+", "{", "}", "[", "]", "_", ":", "`", "|", "<", ">", ",", ".", "?", "/", "~" ) ; 
    HexString          = "0", "x", HexByte { HexByte } ;
    HexByte            = HexNibble, HexNibble ;
    HexNibble          = ( Digit | "a" | "b" | "c" | "d" | "e" | "f" ) ;       (* no uppercase hex allowed *)
    Base58String       = Base58Char, { Base58Char } ;
    Base58Char         = ( NaturalDigit | Base58UpperChar | Base58LowerChar ) ; 
    Base58UpperChar    = ( "A", "B", "C", "D", "E", "F", "G", "H", "J", "K", "L", "M", "N", "P", "Q", "R", "S", "T", "U", "V", "W", "X", "Y", "Z" ) ;     (* missing I, O *)
    Base58LowerChar    = ( "a", "b", "c", "d", "e", "f", "g", "h", "i", "j", "k", "m", "n", "o", "p", "q", "r", "s", "t", "u", "v", "w", "x", "y", "z" ) ; (* missing l *)
    Digit              = ( "0" | NaturalDigit ) ;
    NaturalDigit       = ( "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ) ;
    EscapeChar         = "\" ;
```

| Rule               | Explanation                                                                                                   |
| -----------------: | :------------------------------------------------------------------------------------------------------------ |
| EPASA              | This is a layer-2 address, fully backwards compatible as Layer-1 address                                      |
| PASA               | This is the standard layer-1 address of the receiver account (account number or account name)                 |
| Checksum           | This is the standard layer-1 address checksum                                                                 |
| ExtendedAddress    | The optional extra text that forms part of layer-2 address (payload specification)                            |
| PublicPayload      | A payload which is not encrypted and publically visible                                                       |
| ReceiverEncPayload | A payload which is ECIES encrypted using receivers public key (the PASA portion specifies receiver)           |
| SenderEncPayload   | A payload which is ECIES encrypted using the senders public key (only sender can decrypt EPASA)               |
| PasswordEncPayload | A payload which is AES256 encrypted using the specified password                                              |
| Payload            | The actual payload data, specified it an well-defined encoding                                                |
| PayloadChecksum    | An UINT16 specified by two hexbytes (4 hexnibbles) that denotes a checksum of payload, used to ensure payload consistency (prevents typo/copy-paste errors)  |
| Password           | The password used in PasswordEndPayload. Must be specified as a SafeAnsiString (chars 32..126)                |
| Pascal64String     | An ANSI string involving a limited subset used for account names (cannot start with a digit)                  |
| SafeAnsiString     | An ANSI string involvolving subset characters 32..126                                                         |
| Base58String       | A Base58-encoded string. This is used for specifying public keys, and hashes of public keys                   |
| HexString          | A hexadecimal-encoded string prefixed with a 0x. Every byte specified by two hexdigits, lower-case            |


**NOTES**: 
 * Text payload and passwords are restricted to ANSI charset subset range 32..126
 * The following characters **\\**, **"**, and **}** must be escaped in ASCII payloads/AES passwords via preceding **\\**

#### Validation Rules

#### AccountNumber Checksum

Layer-1 account checkum must be the following number:

```
  Checksum = ((AccountNumber*101) MOD 89) + 10
```

**NOTE** AccountNumber above denotes the integer portion of the string.

#### Pascal64String 

These strings are used to denote an account names and conform to the following rules.
- By definition, they must **not** start with a digit. 
- String length must between 3..64 inclusive.

#### Payload Checksum

In order to avoid data entry errors, the payload portion is **optionally** checksummed. The payload checksum is calculated as follows:

```
    PayloadChecksum = ToHexStringLE ( CastToUINT16( MurMur3( ToAsciiBytes ( PayloadEncType ++ Payload ) ) MOD 65536 ) )

    where
       PayloadEncType  = "1" for PublicPayload
                       = "2" for ReceiverEncPayload
                       = "3" for SenderEncPayload
                       = "4" for PasswordEncPayload
       ToAsciiBytes    = converts ASCII string argument into raw byte form, character by character (no endianness concerns here)                
       MurMur3         = performs 32bit MurMur3 hash of the byte array argument                
       CastToUINT16    = casts the integer argument into to a 16bit unsigned integer (should never overflow due to modulo 65536)
       ToHexStringLE   = converts the 16bit unsigned integer argument into 4 hexadecimal characters in little-endian
```

#### Payload Lengths

The following validation rules must be applied to Payload lengths

|  Payload Type        | Encryption Mode  |  Byte-form Length   | E-PASA string-form length  |
| :------------------- | :--------------- | :------------------ | :------------------------- |
| ASCII                | None (Public)    | 255                 | 255                        |
| ASCII                | ECIES            | 144                 | 144                        |
| ASCII                | AES              | 223                 | 223                        |
| Hexadecimal          | None (Public)    | 255                 | 510+2                      |
| Hexadecimal          | ECIES            | 144                 | 288+2                      |
| Hexadecimal          | AES              | 223                 | 446+2                      |
| Base58               | None (Public)    | 255                 | 348                        |
| Base58               | ECIES            | 144                 | 196                        |
| Base58               | AES              | 223                 | 304                        |

**NOTE:** +2 accounts for "0x" prefix for hexadecimal strings
 
## E-PASA Examples

### Base Cases 

<table>
<thread>
    <th width="350">E-PASA</th>
    <th>Description</th>
</thead>
<tbody>
    <tr><td>123456-77</td><td>Account 123456-77 (backwards compatible with current addresses)</td></tr>
    <tr><td>pascalcoin-foundation</td><td>Account with name 'pascalcoin-foundation' no payload</td></tr>
    <tr><td>my-favorite-exchange("herman@email.com")</td><td>An account called "my-favorite-exchange" with a recipient-encrypted payload (e.g. an exchange deposit address where only exchange can see payload, used as a user ID)</td></tr>
</tbody>
</table>


### With ASCII payloads 

<table>
<thread>
    <th width="350">E-PASA</th>
    <th>Description</th>
</thead>
<tbody>
    <tr><td>123456-77</td><td>Account 123456-77 (backwards compatible)</td></tr>
    <tr><td>123456-77["Hello World!"]</td><td>With public ASCII payload "Hello World!" without checksum protection</td></tr>
    <tr><td>123456-77["Hello World!"]:10cb</td><td>Checksum protected</td></tr>
    <tr><td>123456-77("Hello World!"):7ba2</td><td>ECIES encrypted using <b>recipients</b> public key</td></tr>
    <tr><td>123456-77<"Hello World!">:b51f</td><td>ECIES encrypted using <b>senders</b> public key</td></tr>
    <tr><td>123456-77{"Hello World!":!43lp|-d|a%@#!}</td><td>AES256 encrypted payload using password <b>!43lp|-d|a%@#!</b></td></tr>
</tbody>
</table>

### With Hexadecimal payloads 

<table>
<thread>
    <th width="350">E-PASA</th>
    <th>Description</th>
</thead>
<tbody>
    <tr><td>77-44[0x416c70686124]</td><td>Account 77-44 with unencrypted (public) hexadecimal payload <b>without</b> protection</td></tr>
    <tr><td>77-44[0x416c70686124]:10cb</td><td>Checksum protected</td></tr>
    <tr><td>77-44(0x416c70686124):7ba2</td><td>ECIES encrypted using <b>recipients</b> public key (and checksum protected)</td></tr>
    <tr><td>77-44<0x416c70686124>:b51f</td><td>ECIES encrypted using <b>senders</b> public key (and checksum protected)</td></tr>
    <tr><td>77-44{0x416c70686124:!43lp-da%@#!}</td><td>AES encrypted using password <b>!43lp-da%@#!</b></td></tr> 
</tbody>
</table>


### With Base58 payloads 

<table>
<thread>
    <th width="500">E-PASA</th>
    <th>Description</th>
</thead>
<tbody>
    <tr><td>77-44[1BvBMSEYstWetqTFn5Au4m4GFg7xJaNVN2]</td><td>Account 77-44 with unencrypted (public) Base58 payload (bitcoin address) <b>without</b> checksum protection</td></tr>
    <tr><td>77-44[1BvBMSEYstWetqTFn5Au4m4GFg7xJaNVN2]:10cb</td><td>Checksum protected</td></tr>
    <tr><td>77-44(1BvBMSEYstWetqTFn5Au4m4GFg7xJaNVN2):7ba2</td><td>ECIES encrypted using <b>recipients</b> public key</td></tr>
    <tr><td>77-44<1BvBMSEYstWetqTFn5Au4m4GFg7xJaNVN2>:b51f</td><td>ECIES encrypted using <b>senders</b> public key</td></tr>
    <tr><td>77-44{1BvBMSEYstWetqTFn5Au4m4GFg7xJaNVN2:!43lp-da%@#!}</td><td>AES encrypted using password <b>!43lp-da%@#!</b></td></tr> 
</tbody>
</table>


### Special Cases 

<table>
<thread>
    <th width="500">E-PASA</th>
    <th>Description</th>
</thead>
<tbody>
    <tr><td>999-72["Message with all escaped chars \\\"\} here"]</td><td>Public ANSI string <b>Message with all escaped chars \"} here</b></td></tr>
    <tr><td>999-72[]</td><td>Empty public payload (all zeros)</td></tr>
    <tr><td>999-72[]:10cb</td><td>Empty public payload with checksum protection</td></tr>
    <tr><td>999-72():7ba2</td><td>ECIES encrypted empty payload using <b>recipients</b> public key</td></tr>
    <tr><td>999-72<>:b51f</td><td>ECIES encrypted empty payload using <b>senders</b> public key</td></tr>
    <tr><td>999-72{:Alpha1%}</td><td>AES encrypted empty payload using password <b>Alpha1%</b></td></tr> 
    <tr><td>999-72{"Hello":Funny\"Pwd}</td><td>AES encrypted using escaped password <b>Funny"Pwd</b></td></tr> 
    <tr><td>999-72{"Hello":\\\"\}}</td><td>AES encrypted using escaped password <b>\"}</b></td></tr>
</tbody>
</table>

## Rationale

The design approach was to remain backwards compatible so that EPASA can replace "account" in existing JSON APIs. The caller need not specify Payloads anymore since the EPASA can contain the Payload.


## Backwards Compatibility

This PIP is backwards compatible and does not require a hard-fork activation, only an implementation change.

## Acknowledgements
* Ugochukwu Mmaduekwe for assistance developing payload length validation rules
* Benjamin Ansbach for regular feedback, assistance and insightful suggestions
* UrbanCohort for elegancy-improving suggestion
 
## Reference Implementation

WIP

## Links

None
